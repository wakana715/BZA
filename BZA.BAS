10000 'BASIC Z80 ASSEMBLER BZA.BAS V1.0
10010 'INPUT BZA.ASM OUTPUT BZA1.SYM, BZA2.BAS, BZA3.LST
10020 DIM Z$(699):Y%=699:Z$(Y%)="":D%=1
10030 *ZL1:P%=0:C$="":B$=""
10040 IF D%<>2 THEN GOTO *ZL3
10050 CLOSE #1:OPEN "BZA1.SYM" FOR OUTPUT AS #1:I%=Y%
10060 *ZL2:IF LEN(Z$(I%))<1 THEN GOTO *ZL3
10070 PRINT #1,LEFT$(Z$(I%),4);" ";MID$(Z$(I%),5):I%=I%+1:GOTO *ZL2
10080 *ZL3:CLOSE #1:OPEN "BZA.ASM" FOR INPUT AS #1:N%=0
10090 IF D%=2 THEN CLOSE #2:OPEN "BZA2.BAS" FOR OUTPUT AS #2:GOSUB *DUMPS
10100 IF D%=3 THEN CLOSE #2:OPEN "BZA3.LST" FOR OUTPUT AS #2
10110 *ZL4:E%=0:LINE INPUT #1,A$:GOSUB *ZMA1:GOSUB *LST
10120 IF E%>&HA00 THEN GOTO *ZMV
10130 IF EOF(1)=0 THEN GOTO *ZL4
10140 IF D%=2 THEN GOSUB *DUMPE
10150 D%=D%+1:IF D%<4 THEN GOTO *ZL1
10160 CLOSE #1:CLOSE #2:PRINT "SUCCESS":END
10170 *ZMV:CLOSE #1:CLOSE #2:PRINT "MEMORY OVER E=";E%;",X=";X%;"Y=";Y%:END
10180 *ZMA1:J%=LEN(A$):I%=1:X%=0
10190 *ZMA2:IF X%>0 THEN GOSUB *ZSB1
10200 *ZMA3:Z$(0)="":X%=0
10210 *ZMA4:GOSUB *ZSP1:IF I%<=J% THEN GOTO *ZTK1
10220 IF X%>0 THEN GOTO *ZSB1
10230 RETURN
10240 *ZTK1:F$=MID$(A$,I%,1):I%=I%+1
10250 IF F$=":" THEN GOTO *ZTK7
10260 IF F$<>";" THEN GOTO *ZTK3
10270 IF LEN(Z$(X%))<1 THEN GOTO *ZTK2
10280 X%=X%+1:IF X%+1>=Y% THEN E%=&HA01:RETURN
10290 *ZTK2:F$=F$+MID$(A$,I%):Z$(X%)=F$:I%=J%+1
10300 GOTO *ZMA2
10310 *ZTK3:IF F$=" " OR F$=CHR$(9) THEN GOTO *ZTK8
10320 IF F$=CHR$(34) OR F$=CHR$(39) THEN GOTO *ZST1
10330 K%=INSTR(",()=&|+-*/%",F$):IF K%=0 THEN GOTO *ZTK6
10340 IF LEN(Z$(X%))>0 THEN X%=X%+1:IF X%+1>=Y% THEN E%=&HA02:RETURN
10350 K%=LEN(F$):G$="":L%=1
10360 *ZTK4:IF L%>K% THEN GOTO *ZTK5
10370 M%=ASC(MID$(F$,L%,1)):M%=M%+(M%>&H60 AND M%<&H7B)*&H20:G$=G$+CHR$(M%):L%=L%+1
10380 GOTO *ZTK4
10390 *ZTK5:Z$(X%)=G$:X%=X%+1:IF X%+1>=Y% THEN E%=&HA03:RETURN
10400 Z$(X%)="":IF I%>J% THEN GOTO *ZMA2
10410 GOTO *ZMA4
10420 *ZTK6:Z$(X%)=Z$(X%)+F$:IF I%<=J% THEN GOTO *ZTK1
10430 *ZTK7:X%=X%-(LEN(Z$(X%))>0):Z$(X%)="":GOTO *ZMA2 'FIND LAST
10440 *ZTK8:X%=X%+1:IF X%+1>=Y% THEN E%=&HA04:RETURN
10450 Z$(X%)="":GOTO *ZMA4
10460 *ZST1:G$=F$:Z$(X%)=F$:X%=X%+1:IF X%+1>=Y% THEN E%=&HA05:RETURN
10470 Z$(X%)=""
10480 K%=INSTR(I%,A$,G$):IF K%>I% THEN GOTO *ZST3
10490 *ZST2:IF K%>0 THEN E%=2:RETURN 'NO BODY
10500 E%=1:RETURN 'NOT CLOSE
10510 *ZST3:F$=MID$(A$,I%,K%-I%)
10520 I%=K%+1:Z$(X%)=F$:Z$(X%+1)=G$:X%=X%+2:Z$(X%)=""
10530 IF X%+1>=Y% THEN E%=&HA06:RETURN
10540 GOTO *ZTK1
10550 'SUB
10560 *ZSB1:IF X%>=Y% THEN E%=&H601:RETURN
10570 F$=Z$(0):GOSUB *ZNS:IF K%>0 THEN RETURN
10580 F$=Z$(0):L%=LEN(F$)
10590 FOR K%=1 TO L%:G$=MID$(F$,K%,1)
10600 M%=((G$>="A" AND G$<="Z") OR (K%>1 AND G$>="0" AND G$<="9") OR G$="_")
10610 IF M%=0 THEN E%=3:RETURN
10620 NEXT
10630 IF X%<3 THEN GOTO *ZSB2
10640 G$=Z$(1):IF G$="=" OR G$="EQU" THEN GOTO *ZEQ
10650 *ZSB2:
10660 IF D%<>1 THEN GOTO *ZSB3
10670 IF Y%-1<=X% THEN E%=&HA07:RETURN
10680 GOSUB *ZFS1:IF LEN(G$)>0 THEN E%=&H602:RETURN
10690 Y%=Y%-1: G$=RIGHT$("0000"+HEX$(P%),4):Z$(Y%)=G$+F$
10700 *ZSB3:IF X%>1 AND E%=0 THEN E%=4:RETURN
10710 IF D%>2 THEN C$=C$+RIGHT$("0000"+HEX$(P%),4)
10720 IF X%>1 THEN FOR K%=1 TO X%:Z$(K%-1)=Z$(K%):NEXT:X%=X%-1:GOTO *ZSB1
10730 RETURN
10740 *ZSP1:F$=MID$(A$,I%,1):IF F$<>" " AND F$<>CHR$(9) THEN RETURN
10750 I%=I%+1:IF I%<=J% THEN GOTO *ZSP1
10760 RETURN
10770 *ZFS1:T%=Y%:G$="" 'FIND SYMBOL
10780 *ZFS2:IF LEN(Z$(T%))<1 THEN RETURN 'NOT FOUND
10790 IF F$=MID$(Z$(T%),5) THEN G$=MID$(Z$(T%),1,4):RETURN 'MATCH
10800 T%=T%+1:GOTO *ZFS2
10810 'MNEMONIC-SUB
10820 *ZNS:IF X%<2 THEN GOTO *ZNN
10830 Q%=        INSTR("LD EX BIT RES SET JP CALL IM DJNZ JR OR CP SUB AND XOR POP PUSH RET RST ADD ADC SBC IN OUT INC DEC ORG",F$)
10840 IF Q%=0 THEN GOTO *ZNS1
10850 R%=VAL(     MID$("400400400040004000200200002002000020020020020002000200020002000020002000400040004000600600020002000200",Q%,1))
10860 IF R%=0 THEN Q%=0:GOTO *ZNS1
10870 K%=VAL("&H"+MID$("40 00 40  80  C0  00 00   00 10   18 B0 B8 90  A0  A8  C1  C5   C0  C7  09  4A  42  40 41  04  05  00",Q%,2))
10880 L%=VAL("&H"+MID$("00 00 00  00  00  00 00   00 00   00 00 00 00  00  00  00  00   00  00  80  88  98  01 05  03  0B  00",Q%,2))
10890 S%=VAL(     MID$("01 02 03  03  03  04 05   06 07   08 09 09 09  09  09  10  10   11  12  13  13  13  14 14  15  15  16",Q%,2))
10900 IF X%<R% THEN E%=5:K%=1:RETURN
10910 ON S% GOTO *ZLD,*ZEX,*ZBT,*ZJP,*ZCA,*ZIM,*ZDJ,*ZJR,*ZCP,*ZPP,*ZRT,*ZRS,*ZAS,*ZIT,*ZID,*ZRG
10920 *ZNS1:L%=1:GOSUB *ZSR:IF K%>0 THEN RETURN
10930 K%=INSTR("DBDW",F$):IF K%>0 THEN GOTO *ZDB
10940 RETURN
10950 *ZLD:F$=Z$(1):GOSUB *GREG:L%=V%:IF V%>7 THEN GOTO *ZLD08
10960 F$=Z$(3):GOSUB *GREG:IF V%<8 THEN GOTO *ZKLV 'LD B,C
10970 IF L%<>7 THEN GOTO *ZLD01
10980 M%=INSTR("IR",F$):IF M%>0 THEN B$=B$+"ED5"+MID$("7F",M%,1):K%=1:RETURN 'LD A,I:LD A,R
10990 *ZLD01:IF V%<>8 THEN GOTO *ZLD02
11000 IF X%<6 OR Z$(X%-1)<>")" THEN E%=6:RETURN
11010 GOTO *ZLD04 'LD C,(HL... IX...
11020 *ZLD02:IF X%<9 THEN GOTO *ZLD03
11030 M%=L%:L%=4:F$=Z$(3):GOSUB *ZSR:IF K%>0 THEN RETURN 'LD B,RLC (IX+d)
11040 *ZLD03:IF X%>3 THEN W%=3:K%=0:V%=6:GOSUB *ZKLV:GOTO *ZR08 'LD B,7
11050 *ZLD04:F$=Z$(4):IF L%<>7 THEN GOTO *ZLD05
11060 V%=INSTR("BCDEHL",F$):IF V%>0 THEN B$=B$+MID$("0A1A7E",V%,2):K%=1:RETURN 'LD A,(BC... DE... HL)
11070 *ZLD05:GOSUB *CXY:IF V%>7 THEN GOTO *ZLD06
11080 IF X%<8 THEN E%=7:RETURN
11090 GOTO *ZLD07 'LD C,(IX+d)
11100 *ZLD06:IF L%=7 THEN W%=3:B$=B$+"3A":GOTO *ZR16 'LD A,(F000H)
11110 IF F$<>"HL" THEN E%=&H201:RETURN
11120 V%=6:GOTO *ZKLV 'LD B,(HL)
11130 *ZLD07:GOSUB *GXY:W%=3:K%=&H46:V%=0:GOSUB *ZKLV:GOTO *ZR08 'LD B,(IX+2)
11140 *ZLD08:GOSUB *GR16:IF V%=2 THEN GOTO *ZLD10
11150 IF V%>7 THEN GOTO *ZLD12
11160 L%=V%*16:F$=Z$(3):IF V%<>3 THEN GOTO *ZLD09
11170 GOSUB *GHL:IF V%<>9 THEN B$=B$+"F9":K%=1:RETURN 'LD SP,HL
11180 *ZLD09:IF F$<>"(" THEN W%=3:B$=B$+RIGHT$("0"+HEX$(L%+1),2):GOTO *ZR16 'LD BC,F000H
11190 IF X%<6 THEN E%=8:RETURN
11200 W%=3:B$=B$+"ED"+RIGHT$("0"+HEX$(L%+&H4B),2):GOTO *ZR16 'LD BC,(F000H)
11210 *ZLD10:IF Z$(X%-1)<>")" THEN GOTO *ZLD11
11220 IF X%<6 THEN E%=9:RETURN
11230 W%=3:B$=B$+"2A":GOTO *ZR16 'LD HL,(F000H)
11240 *ZLD11:W%=3:B$=B$+"21":GOTO *ZR16 'LD HL,F000H
11250 *ZLD12:M%=INSTR("IR",F$):IF M%>0 THEN B$=B$+"ED4"+MID$("7F",M%,1):K%=1:RETURN 'LD I,A:LD R,A
11260 IF V%<>8 THEN E%=&H101:RETURN
11270 IF X%<6 THEN E%=&H10:RETURN
11280 F$=Z$(2):V%=INSTR("BCDEHL",F$):IF V%=5 THEN GOTO *ZLD13
11290 IF V%>0 AND Z$(5)="A" THEN B$=B$+MID$("0212",V%,2):K%=1:RETURN
11300 GOSUB *CXY:IF V%<8 THEN GOTO *ZLD15
11310 IF Z$(X%-1)="A" THEN W%=1:B$=B$+"32":GOTO *ZR16 'LD (F000H),A
11320 F$=Z$(X%-1):GOSUB *GR16:IF V%>7 THEN E%=&H102:RETURN
11330 IF V%<>2 THEN B$=B$+"ED"+RIGHT$("0"+HEX$(&H43+V%*16),2):W%=1:GOTO *ZR16 'LD (F000H),BC
11340 W%=1:B$=B$+"22":GOTO *ZR16
11350 *ZLD13:F$=Z$(5):GOSUB *GREG:IF V%>7 THEN W%=5:B$=B$+"36":GOTO *ZR08 'LD (HL),12
11360 *ZLD14:B$=B$+RIGHT$("0"+HEX$(&H70+V%),2):K%=1:RETURN 'LD (HL),B
11370 *ZLD15:IF X%<8 THEN E%=&H11:RETURN
11380 GOSUB *GXY:F$=Z$(7):GOSUB *GREG:IF V%>7 THEN W%=1:B$=B$+"36":GOSUB *ZR08:W%=W%+1:GOTO *ZR08 'LD (IY+9),10
11390 GOSUB *ZLD14:W%=1:GOTO *ZR08 'LD (IX+5),C
11400 *ZEX:M%=INSTR("DEAF",Z$(1)):IF M%>0 THEN B$=B$+MID$("EB08",M%,2):K%=1:RETURN 'EX DE,HL:EX AF,AF'
11410 IF X%<6 THEN E%=&H12:K%=1:RETURN
11420 F$=Z$(5):H$="E3":K%=1:GOTO *ZJP1 'EX (SP),HL:EX (SP),IX:EX (SP),IY
11430 *ZPP:F$=Z$(1):GOSUB *GR16:IF F$="AF" THEN V%=3
11440 IF V%>7 THEN E%=&H103:RETURN
11450 GOTO *ZPP9
11460 *ZSR:K%=   INSTR("RL RR RLC RRC SLA SRA SRL SLI",F$):IF K%=0 THEN RETURN
11470 IF          MID$("-  -  -   -   -   -   -   -  ",K%,1)<>"-"  THEN K%=0:RETURN
11480 K%=VAL("&H"+MID$("10 18 00  08  20  28  38  30",K%,2)):F$=Z$(L%):GOSUB *GREG:IF V%<8 THEN H$="":M%=V%:GOTO *ZSR1
11490 IF X%<4 THEN E%=&H13:K%=1:RETURN
11500 IF V%>8 THEN E%=&H104:K%=1:RETURN
11510 F$=Z$(L%+1):IF F$="HL" THEN H$="":M%=6:GOTO *ZSR1 'RL (HL)
11520 IF X%<6 THEN E%=&H14:RETURN
11530 GOSUB *GXY:IF V%>7 THEN E%=&H105:RETURN
11540 IF L%=1 THEN M%=6 'RR (IX+d) L%=4 THEN LD B,RR (IX+d)
11550 IF D%=1 THEN H$="00":GOTO *ZSR1
11560 W%=L%+1:GOSUB *EVAL
11570 *ZSR1:V%=M%:L%=0:B$=B$+"CB"+H$:GOSUB *ZKLV:RETURN
11580 *ZAS:IF F$<>"ADD" OR Z$(1)<>Z$(3) THEN GOTO *ZAS1
11590 F$=Z$(1):GOSUB *GXY:IF V%<8 THEN B$=B$+"29":K%=1:RETURN
11600 *ZAS1:F$=Z$(1):GOSUB *GR16:IF V%>7 THEN GOTO *ZAS2
11610 IF V%<>2 THEN GOTO *ZAS2
11620 F$=Z$(3):GOSUB *GR16:IF V%>7 THEN E%=&H106:RETURN 'ADD HL,DE
11630 IF Z$(0)<>"ADD" THEN B$=B$+"ED"
11640 *ZPP9:B$=B$+RIGHT$("0"+HEX$(K%+V%*16),2):RETURN 'PUSH BC
11650 *ZAS2:K%=L%:L%=3:M%=6:GOTO *ZCP1
11660 *ZCP:L%=1:M%=4
11670 *ZCP1:F$=Z$(L%):GOSUB *GREG:IF V%<8 THEN H$="":L%=0:GOTO *ZKLV
11680 IF V%>8 THEN W%=L%:L%=0:V%=&H46:GOSUB *ZKLV:GOTO *ZR08 'OR N
11690 IF X%<M% THEN E%=&H15:RETURN
11700 F$=Z$(L%+1):IF F$="HL" THEN H$="":GOTO *ZCP2
11710 IF X%<M%+2 THEN E%=&H16:RETURN
11720 GOSUB *GXY:IF V%>8 THEN E%=&H107:RETURN
11730 IF D%=1 THEN H$="00":GOTO *ZCP2
11740 W%=L%:GOSUB *EVAL 'SUB (IX+d)
11750 *ZCP2:B$=B$+RIGHT$("0"+HEX$(K%+6),2)+H$:K%=1:RETURN
11760 *ZIT:W%=1:IF K%=&H40 THEN W%=3
11770 F$=Z$(L%):IF Z$(W%)="(" AND Z$(W%+1)="C" THEN GOTO *ZIT1 'IN A,(C):OUT (C),A
11780 IF Z$(W%)<>"(" OR F$<>"A" THEN E%=&H108:RETURN
11790 IF D%=1 THEN B$=B$+"0000":K%=1:RETURN
11800 GOSUB *EVAL:L%=&HD3:IF K%=&H40 THEN L%=&HDB
11810 B$=B$+HEX$(L%)+H$:K%=1:RETURN
11820 *ZIT1:GOSUB *GREG:IF V%<8 THEN B$=B$+"ED":L%=V%:V%=0:GOTO *ZKLV
11830 IF K%=&H40 AND F$="F" THEN B$=B$+"ED70":K%=1:RETURN 'IN F,(C)
11840 IF K%=&H41 AND F$="0" THEN B$=B$+"ED71":K%=1:RETURN 'OUT (C),0
11850 E%=&H109:RETURN
11860 *ZID:F$=Z$(1):GOSUB *GREG:IF V%<8 THEN H$="":L%=V%:GOTO *ZID2
11870 IF V%<9 THEN GOTO *ZID1
11880 GOSUB *GR16:IF V%>7 THEN E%=&H110:RETURN
11890 K%=L%:L%=0:V%=V%*16:GOTO *ZKLV 'INC BC
11900 *ZID1:IF X%<4 THEN E%=&H17:RETURN
11910 F$=Z$(2):IF F$="HL" THEN H$="":L%=6:GOTO *ZID2
11920 IF X%<6 THEN E%=&H18:RETURN
11930 GOSUB *GXY:IF V%>8 THEN E%=&H111:RETURN
11940 W%=1:GOSUB *EVAL:L%=6
11950 *ZID2:V%=0:GOSUB *ZKLV:B$=B$+H$:RETURN 'INC (HL):INC (IX+d)
11960 *ZBT:W%=1:GOSUB *EVAL:L%=V%:IF V%<0 OR V%>7 THEN E%=&H301:RETURN
11970 F$=Z$(3):GOSUB *GREG:IF V%<8 THEN H$="":GOTO *ZBT1 'SET 1,A
11980 IF X%<6 THEN E%=&H19:RETURN
11990 F$=Z$(4):IF F$="HL" THEN H$="":V%=6:GOTO *ZBT1 'SET 0,(HL)
12000 IF X%<8 THEN E%=&H20:RETURN
12010 GOSUB *GXY:IF V%>8 THEN E%=&H112:RETURN
12020 W%=W%+1:GOSUB *EVAL:V%=6 'RES 2,(IX+d)
12030 *ZBT1:B$=B$+"CB"+H$:GOTO *ZKLV
12040 *ZRT:F$=Z$(1):GOSUB *GFLG:IF V%=9 THEN E%=&H401:RETURN
12050 L%=V%:V%=0:GOTO *ZKLV
12060 *ZIM:IF D%=1 THEN B$=B$+"ED00":K%=1:RETURN
12070 W%=1:GOSUB *EVAL:IF V%>=0 AND V%<=2 THEN B$=B$+"ED"+MID$("46565E",V%*2+1,2):K%=1:RETURN
12080 E%=&H501:K%=1:RETURN
12090 *ZRS:IF D%=1 THEN B$=B$+"C7":K%=1:RETURN
12100 W%=1:GOSUB *EVAL
12110 IF V%>0 AND V%<8 THEN L%=V%:V%=0:GOTO *ZKLV
12120 IF (V% AND &H7)=0 THEN L%=V%/8:V%=0:GOTO *ZKLV
12130 E%=&H502:RETURN
12140 *ZJP:IF X%<3 THEN B$=B$+"C3":W%=1:GOTO *ZR16
12150 IF Z$(1)<>"(" THEN K%=&HC2:GOTO *ZJC1
12160 F$=Z$(2):H$="E9"
12170 *ZJP1:GOSUB *GHL:IF V%>7 THEN E%=&H113:K%=1:RETURN
12180 B$=B$+H$:K%=1:RETURN
12190 *ZCA:IF X%<3 THEN B$=B$+"CD":W%=1:GOTO *ZR16
12200 K%=&HC4
12210 *ZJC1:F$=Z$(1):GOSUB *GFLG:IF V%=9 THEN E%=&H402:RETURN
12220 L%=V%:V%=0:W%=3:GOSUB *ZKLV:GOTO *ZR16
12230 *ZJR:IF X%<3 THEN GOTO *ZDJ
12240 F$=Z$(1):GOSUB *GFLG:IF V%=9 THEN GOTO *ZDJ 'JR L1
12250 K%=&H20:M%=V%:W%=3:GOTO *ZDJ1 'JR NZ,L1
12260 *ZDJ:M%=0:W%=1
12270 *ZDJ1:IF D%=1 THEN L%=0:GOTO *ZDJ2
12280 GOSUB *EVAL:W%=V%-P%-2:IF W%>127 OR W%<-128 THEN E%=&H701:RETURN
12290 *ZDJ2:L%=M%:V%=0:GOSUB *ZKLV:B$=B$+RIGHT$("0"+HEX$(W%),2):RETURN
12300 *ZNN:
12310 K%=INSTR("DI EI NOP RLA RRA DAA CPL SCF CCF RET EXX RLCA RRCA HALT",F$):IF K%=0 THEN GOTO *ZNN1
12320 IF  MID$("-  -  -   -   -   -   -   -   -   -   -   -    -    -   ",K%,1)<>"-"  THEN GOTO *ZNN1
12330 F$= MID$("F3 FB 00  17  1F  27  2F  37  3F  C9  D9  07   0F   76",K%,2):B$=B$+F$:K%=1:RETURN
12340 *ZNN1
12350 K%=INSTR("NEG RRD RLD LDI CPI INI LDD CPD IND RETN RETI OUTI OUTD LDIR CPIR INIR OTIR LDDR CPDR INDR OTDR",F$):IF K%=0 THEN RETURN
12360 IF  MID$("-   -   -   -   -   -   -   -   -   -    -    -    -    -    -    -    -    -    -    -    -   ",K%,1)<>"-"  THEN RETURN
12370 F$= MID$("44  67  6F  A0  A1  A2  A8  A9  AA  45   4D   A3   AB   B0   B1   B2   B3   B8   B9   BA   BB",K%,2):B$=B$+"ED"+F$:K%=1:RETURN
12380 *ZKLV:B$=B$+RIGHT$("0"+HEX$(K%+L%*8+V%),2):K%=1:RETURN
12390 *ZR08:IF D%=1 THEN B$=B$+"00"  :K%=1:RETURN
12400 GOSUB *EVAL:B$=B$+H$:RETURN
12410 *ZR16:IF D%=1 THEN B$=B$+"0000":K%=1:RETURN
12420 GOSUB *EVAL:B$=B$+G$:K%=1:RETURN
12430 *GREG 'REG8
12440 V%=   INSTR("ALHEDCB(",F$):IF V%=0 THEN GOTO *GRE1
12450 V%=VAL(MID$("75432108",V%,1)):RETURN
12460 *GRE1 'REG8 IXH
12470 V%=   INSTR("IXIYIXHIXLIYHIYL",F$):IF V%<5 THEN V%=9:RETURN
12480 B$=B$+ MID$("    DD DD FF FF",V%,1)+"D"
12490 V%=VAL(MID$("    44 55 44 55",V%,1)):RETURN
12500 *GR16 'REG16
12510 V%=   INSTR("BCDESP(",F$):IF V%=0 THEN GOTO *GHL
12520 V%=VAL(MID$("0 1 3 8",V%,1)):RETURN
12530 *GHL:IF F$="HL" THEN V%=2:RETURN
12540 *GXY:V%=INSTR("IIXIY",F$):IF V%<2 THEN V%=9:RETURN
12550    B$=B$+MID$(" DDFF",V%,1)+"D":V%=2:RETURN
12560 *CXY:IF F$="IX" OR F$="IY" THEN V%=2:RETURN
12570 V%=9:RETURN
12580 *GFLG 'FLG
12590 V%=   INSTR("ZCPMNZNCPOPE",F$):IF V%=0 THEN V%=9:RETURN
12600 V%=VAL(MID$("13670 2 4 5",V%,1)):RETURN
12610 'MACRO SUB
12620 *ZRG:W%=1:GOSUB *EVAL:P%=V%:IF D%>1 THEN C$=C$+RIGHT$("0000"+HEX$(P%),4)
12630 K%=1:RETURN
12640 *ZEQ:W%=2:GOSUB *EVAL:IF D%<>1 THEN GOTO *ZEQ2
12650 IF Y%-1<=X% THEN E%=&HA08:RETURN
12660 F$=Z$(0):GOSUB *ZFS1:IF LEN(G$)>0 THEN E%=&H603:RETURN
12670 Y%=Y%-1:Z$(Y%)=RIGHT$("000"+HEX$(V%),4)+Z$(0)
12680 *ZEQ2:IF E%>0 THEN RETURN
12690 IF D%>2 THEN C$=C$+MID$(G$,3)+LEFT$(G$,2)
12700 K%=1:RETURN
12710 *ZDB:W%=1 'DB IN K%=1:DB,3:DW
12720 *ZDB1:IF W%>=X% THEN GOTO *ZDB3
12730 F$=Z$(W%)
12740 IF F$=CHR$(34) OR F$=CHR$(39) THEN GOSUB *ZDB4:GOTO *ZDB2
12750 GOSUB *EVAL:W%=W%+1:IF K%>1 OR V%<0 OR V%>255 THEN H$=G$
12760 B$=B$+H$
12770 *ZDB2:IF E%>0 THEN RETURN
12780 GOTO *ZDB1
12790 *ZDB3:K%=5:RETURN
12800 *ZDB4:F$=Z$(W%+1):L%=LEN(F$):FOR M%=1 TO L%:B$=B$+RIGHT$("0"+HEX$(ASC(MID$(F$,M%,1))),2):NEXT:W%=W%+4:RETURN
12810 'DUMP,LST
12820 *LST:IF D%=1 THEN P%=P%+(LEN(B$)/2):B$="":RETURN
12830 IF D%=2 THEN GOTO *DUMP
12840 F$="":G$=B$:IF E%>0 THEN F$=MID$("SIRBFPUJEDM",(E%/256)+1,1)+RIGHT$("0"+HEX$(E% AND &HFF),2)+" "
12850 N%=N%+1:F$=F$+C$:C$="":IF LEN(F$)<1 AND LEN(G$)>0 THEN F$=RIGHT$("0000"+HEX$(P%),4)
12860 P%=P%+(LEN(B$)/2):B$="":IF E%>0 THEN *LST1
12870 H$=RIGHT$("     "+STR$(N%),5)+" "+LEFT$(F$+"    ",4)+" "+LEFT$(G$+SPACE$(12),12)+" "+A$
12880 PRINT #2,H$:GOTO *LST2
12890 *LST1:
12900 H$=RIGHT$("     "+STR$(N%),5)+"*"+LEFT$(F$+"    ",4)+" "+LEFT$(G$+SPACE$(12),12)+" "+A$
12910 PRINT #2,H$:PRINT H$
12920 *LST2
12930 IF LEN(F$)>0 THEN F$=MID$(F$,5)
12940 IF LEN(G$)>0 THEN G$=MID$(G$,13)
12950 IF LEN(F$)<1 AND LEN(G$)<1 THEN RETURN
12960 PRINT #2,SPACE$(6)               ;LEFT$(F$+"    ",4);" ";LEFT$(G$+SPACE$(12),12)
12970 GOTO *LST2
12980 *DUMP:IF LEN(C$)>0 THEN GOTO *DUMP1
12990 IF LEN(B$)<=32 THEN RETURN
13000 PRINT #2,MID$(STR$(1000+N%),1,4);"0 DATA 0,";LEFT$(B$,32):B$=MID$(B$,33):N%=N%+1:P%=P%+16:RETURN
13010 *DUMP1:IF LEN(B$)>0 THEN PRINT #2,MID$(STR$(1000+N%),1,4);"0 DATA 0,";B$:B$="":N%=N%+1:P%=P%+(LEN(B$)/2)
13020 PRINT #2,MID$(STR$(1000+N%),1,4);"0 DATA 1,";RIGHT$("000"+HEX$(P%),4):N%=N%+1:C$="":RETURN
13030 *DUMPS:N%=8
13040 PRINT #2,"10000 P%=0"
13050 PRINT #2,"10010 READ A%:IF A%=9 THEN END"
13060 PRINT #2,"10020 IF A%=1 THEN READ B$:P%=VAL(";CHR$(34);"&H";CHR$(34);"+B$):GOTO 10010"
13070 PRINT #2,"10030 READ B$:J%=LEN(B$)"
13080 PRINT #2,"10040 FOR I%=1 TO J% STEP 2"
13090 PRINT #2,"10050 POKE P%,VAL(";CHR$(34);"&H";CHR$(34);"+MID$(B$,I%,2)):P%=P%+1"
13100 PRINT #2,"10060 NEXT"
13110 PRINT #2,"10070 GOTO 10010"
13120 RETURN
13130 *DUMPE:IF LEN(B$)>0 THEN PRINT #2,MID$(STR$(1000+N%),1,4);"0 DATA 0,";B$:B$="":N%=N%+1
13140 PRINT #2,MID$(STR$(1000+N%),1,4);"0 DATA 9":RETURN
13150 'EVAL IN W%,X%,Y%,Z$()
13160 *EVAL:Q%=W%
13170 *EVAL01:F$=Z$(Q%):IF F$="" OR F$="," OR Q%>=X% THEN GOTO *EVAL06
13180 IF INSTR("&|+-*/%()",F$)>0 THEN Q%=Q%+1:GOTO *EVAL01
13190 IF F$="IX" OR F$="IY" THEN Z$(Q%)="0":Q%=Q%+1:GOTO *EVAL01
13200 IF F$="$" THEN Z$(Q%)=HEX$(P%):Q%=Q%+1:GOTO *EVAL01
13210 T%=Y% 'FIND SYMBOL
13220 *EVAL02:IF Z$(T%)="" THEN GOTO *EVAL03 'NOT FOUND
13230 IF F$=MID$(Z$(T%),5) THEN Z$(Q%)=MID$(Z$(T%),1,4):Q%=Q%+1:GOTO *EVAL01
13240 T%=T%+1:GOTO *EVAL02 'MATCH
13250 *EVAL03:S%=LEN(F$):R%=0:U%=0:G$=MID$(F$,1,1):H$=RIGHT$(F$,1)
13260 IF G$=CHR$(39) THEN Z$(Q%)="(":Q%=Q%+1:Z$(Q%)=RIGHT$("0"+HEX$(ASC(Z$(Q%))),2):Z$(Q%+1)=")":Q%=Q%+2:GOTO *EVAL01
13270 IF G$="$" THEN R%=1:F$=MID$(F$,2):S%=S%-1
13280 V%=INSTR("HB",H$):IF V%>0 THEN R%=V%:S%=S%-1:F$=MID$(F$,1,S%)
13290 T%=1
13300 *EVAL04:G$=MID$(F$,T%,1)
13310 IF R%=1 AND G$>="A" AND G$<="F" THEN GOTO *EVAL05 'R%=1:HEX
13320 IF R%<2 AND G$>="0" AND G$<="9" THEN GOTO *EVAL05 'R%=0:DEC
13330 IF R%=2 AND G$>="0" AND G$<="1" THEN U%=U%*2:U%=U%+VAL(G$):GOTO *EVAL05 'R%=2:BIT
13340 E%=&H601:GOTO *EVAL22
13350 *EVAL05:T%=T%+1:IF T%<=S% THEN GOTO *EVAL04
13360 IF R%=0 THEN Z$(Q%)=HEX$(VAL(F$)):Q%=Q%+1:GOTO *EVAL01
13370 IF R%=1 THEN Z$(Q%)=         F$  :Q%=Q%+1:GOTO *EVAL01
13380 IF R%=2 THEN Z$(Q%)=HEX$(U%)     :Q%=Q%+1:GOTO *EVAL01
13390 *EVAL06:G$="":Q%=W%:R%=W% 'CONVERT RPN G$:OP STACK,Q%:RP INDEX,R%:RP START
13400 *EVAL07:F$=Z$(W%):IF F$="" OR F$="," OR W%>=X% THEN GOTO *EVAL11
13410 W%=W%+1:T%=INSTR("&|+-*/%(",F$):IF T%=0 THEN GOTO *EVAL09
13420 IF LEN(G$)=0 THEN GOTO *EVAL08
13430 T%=VAL(MID$("11223334",T%,1))
13440 IF INSTR("&|+-*/%", RIGHT$(G$,1))>=T% THEN GOSUB *EVAL20:G$="":GOTO *EVAL08
13450 *EVAL08:G$=G$+F$:GOTO *EVAL07
13460 *EVAL09:IF F$<>")" THEN Z$(Q%)=F$:Q%=Q%+1:GOTO *EVAL07
13470 *EVAL10:IF LEN(G$)=0 THEN GOTO *EVAL07
13480 H$=RIGHT$(G$,1):G$=LEFT$(G$,LEN(G$)-1)
13490 IF H$="(" THEN GOTO *EVAL07
13500 Z$(Q%)=H$:Q%=Q%+1:GOTO *EVAL10
13510 *EVAL11:IF MID$(G$,1,1)="(" THEN E%=&H801:GOTO *EVAL22
13520 GOSUB *EVAL20:S%=R%:T%=R%
13530 *EVAL12:IF S%>=Q% THEN V%=VAL("&H"+Z$(T%-1)):G$=RIGHT$("000"+HEX$(V%),4):H$=MID$(G$,3):G$=H$+MID$(G$,1,2):RETURN
13540 F$=Z$(S%):IF T%>1 THEN V%=VAL("&H"+Z$(T%-2)):U%=VAL("&H"+Z$(T%-1))
13550 IF F$<>"+" AND F$<>"-" THEN GOTO *EVAL17
13560 IF SGN(V%)<>SGN(U%) THEN GOTO *EVAL16
13570 IF V%<>&H8000 THEN GOTO *EVAL14
13580 IF U%=&H8000 THEN V%=0:GOTO *EVAL18
13590 V%=&H7FFF:U%=U%+1:GOTO *EVAL16
13600 *EVAL14:IF U%<>&H8000 THEN GOTO *EVAL15
13610 IF V%=&H8000 THEN V%=0:GOTO *EVAL18
13620 U%=&H7FFF:V%=V%+1:GOTO *EVAL16
13630 *EVAL15:IF V%>0 AND V%>U% AND (&H7FFF-V%)<U% THEN V%=-V%:U%=-U%:GOTO *EVAL16
13640 IF V%<0 AND V%>U% AND (&H8000-V%)>U% THEN V%=-V%:U%=-U%:GOTO *EVAL16
13650 *EVAL16
13660 IF F$="&" THEN V%=V% AND U%:GOTO *EVAL19
13670 IF F$="|" THEN V%=V% OR  U%:GOTO *EVAL19
13680 IF F$="+" THEN V%=V%+U%:GOTO *EVAL19
13690 IF F$="-" THEN V%=V%-U%:GOTO *EVAL19
13700 *EVAL17
13710 IF F$="*" THEN V%=V%*U%:GOTO *EVAL19
13720 IF F$="/" AND U%=0 THEN E%=&H901:GOTO *EVAL22
13730 IF F$="%" AND U%=0 THEN E%=&H902:GOTO *EVAL22
13740 IF F$="/" THEN V%=V%/U%:GOTO *EVAL19
13750 IF F$="%" THEN V%=V% MOD U%:GOTO *EVAL19
13760 *EVAL18:Z$(T%)=F$:T%=T%+1:S%=S%+1:GOTO *EVAL12 '8,16BIT BASIC
13770 *EVAL19:T%=T%-1:Z$(T%-1)=RIGHT$("000"+HEX$(V%),4):S%=S%+1:GOTO *EVAL12 '8,32BIT BASIC SWITCH
13780 *EVAL20:T%=LEN(G$) 'OP-STACK TO VAR-STACK
13790 *EVAL21:IF T%<=0 THEN RETURN
13800 H$=MID$(G$,T%,1):T%=T%-1
13810 IF H$="(" THEN GOTO *EVAL21
13820 Z$(Q%)=H$:Q%=Q%+1:GOTO *EVAL21
13830 *EVAL22:V%=0:G$="0000":H$="00":RETURN
13840 'VARIABLE LIST
13850 '---- -------------------------------
13860 'A$   SRC
13870 'B$   BIN
13880 'C$   ORG,EQU,LABEL,ERROR
13890 '  D% PASS
13900 '  E% ERROR
13910 'F$   EVAL,GREG,GR16,GFLG IN
13920 'G$   EVAL OUT(16BIT)
13930 'H$   EVAL OUT( 8BIT)
13940 '  I% SRC INDEX
13950 '  J% SRC LEN
13960 '  K%,L%,M% WORK,K%=0:NON,1:ASSEMBLED
13970 '  N% LINE NO
13980 'O--- NOT USE
13990 '  P% PC ADR
14000 '  Q%,R%,S%,T% EVAL WORK
14010 '  U# EVAL WORK
14020 '  V% EVAL,GREG,GR16,GHL,GXY,GFLG OUT
14030 '  W% EVAL INDEX IN/OUT, JR,DJNZ WORK
14040 '  X% TOKEN  INDEX
14050 '  Y% SYMBOL INDEX
14060 'Z$() TOKEN,SYMBOL,EVAL ARRAY
14070 '
14080 'N ERR MESSAGE                EXAMPLE
14090 '- --- ---------------------- --------
14100 '0 S01 STRING IS NOT CLOSED   'STR
14110 '0 S02 STRING HAS NO CONTENT  ''
14120 '0 S03 INVALID LABEL FORMAT   A! EQU 0
14130 '0 S04 UNKNOWN MNEMONIC       ABC B,C
14140 '0 S05 NOT ENOUGH OPCODES  5  LD B,
14150 '0 S06 NOT ENOUGH OPCODES  6  LD A,(HL
14160 '0 S07 NOT ENOUGH OPCODES  7  LD B,(IX)
14170 '0 S08 NOT ENOUGH OPCODES  8  LD BC,(
14180 '0 S09 NOT ENOUGH OPCODES  9  LD HL,()
14190 '0 S10 NOT ENOUGH OPCODES 10  LD (F000H),
14200 '0 S11 NOT ENOUGH OPCODES 11  LD (IX+1),
14210 '0 S12 NOT ENOUGH OPCODES 12  EX (SP),
14220 '0 S13 NOT ENOUGH OPCODES 13  RL (HL
14230 '0 S14 NOT ENOUGH OPCODES 14  RR (IX+
14240 '0 S15 NOT ENOUGH OPCODES 15  SBC A,(HL
14250 '0 S16 NOT ENOUGH OPCODES 16  ADD A,(IX+3
14260 '0 S17 NOT ENOUGH OPCODES 17  DEC (HL
14270 '0 S18 NOT ENOUGH OPCODES 18  INC (IX+7
14280 '0 S19 NOT ENOUGH OPCODES 19  SET 1,(HL
14290 '0 S20 NOT ENOUGH OPCODES 20  BIT 2,(IX+10
14300 '1 I01 INVALID REGISTER    1  LD T,0
14310 '1 I02 INVALID REGISTER    2  LD (F000H),RR
14320 '1 I03 INVALID REGISTER    3  PUSH A
14330 '1 I04 INVALID REGISTER    4  RLC T,0
14340 '1 I05 INVALID REGISTER    5  RRC (IZ+1)
14350 '1 I06 INVALID REGISTER    6  ADC HL,RR
14360 '1 I07 INVALID REGISTER    7  ADC A,(IZ+4)
14370 '1 I08 INVALID REGISTER    8  OUT (D),B
14380 '1 I09 INVALID REGISTER    9  OUT (C),R
14390 '1 I10 INVALID REGISTER   10  INC RR
14400 '1 I11 INVALID REGISTER   11  DEC (RR+8)
14410 '1 I12 INVALID REGISTER   12  RES 3,(RR+11)
14420 '1 I13 INVALID REGISTER   13  EX (SP),RR
14430 '2 R01 HL REGISTER ONLY       LD B,(BC)
14440 '3 B01 INVALID BIT NUMBER     RES 9,B
14450 '4 F01 INVALID FLG         1  RET ZZ
14460 '4 F02 INVALID FLG         2  CALL ZZ,NEXT01
14470 '5 P01 INVALID PARAMETER   1  IM 9
14480 '5 P02 INVALID PARAMETER   2  RST -1
14490 '6 U01 UNDEFINED LABEL     1  LD C,UNDEFLABEL
14500 '6 U02 RE-DEFINE LABEL     2
14510 '6 U03 RE-DEFINE LABEL     3
14520 '7 J01 FAR TO JUMP            JR NEXT1
14530 '8 E01 EXPRESSION NOT CLOSED  LD BC,((1+2)+3
14540 '9 D01 DIVIDE BY ZERO         LD BC,1/0
14550 '9 D02 DIVIDE(MOD) BY ZERO    LD BC,1%0
14560 'A M01 MEMORY FULL         1
14570 'A M0*
14580 'A M08 MEMORY FULL         8
14590 '-
